#
# maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>

_linuxprefix=linux46
_extramodules=extramodules-4.6-MANJARO
pkgname=$_linuxprefix-bbswitch
_pkgname=bbswitch
pkgver=0.8
pkgrel=1
pkgdesc="kernel module allowing to switch dedicated graphics card on Optimus laptops"
arch=('i686' 'x86_64')
url="http://github.com/Bumblebee-Project/bbswitch"
license=('GPL')
depends=("$_linuxprefix")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
install=bbswitch.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz"
        '0001-disable-DSM-if-power-resources-are-in-use.patch')
sha256sums=('76cabd3f734fb4fe6ebfe3ec9814138d0d6f47d47238521ecbd6a986b60d1477'
            'e3959d8c3526979b6f684c06aaa061b06466a60e4c5009b2fc57cf8a3fdaa600')

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -Np1 -i "${srcdir}/0001-disable-DSM-if-power-resources-are-in-use.patch"
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  cd ${srcdir}/${_pkgname}-${pkgver}
  # KDIR is necessary even when cleaning
  #make KDIR=/usr/lib/modules/-${_kernver} clean
  make KDIR=/usr/lib/modules/${_kernver}/build
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  install -D -m644 bbswitch.ko $pkgdir/usr/lib/modules/${_extramodules}/bbswitch.ko
  # gzip -9 modules
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" $startdir/*.install
}
